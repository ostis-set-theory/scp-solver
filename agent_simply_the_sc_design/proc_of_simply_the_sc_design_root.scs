scp_program -> proc_of_simply_the_sc_design_root (*	
	
	-> rrel_params: ...(*
		-> rrel_1: rrel_in: _state;;
		-> rrel_2: rrel_in: _contour;;
		-> rrel_3: rrel_out: rrel_assign: rrel_scp_var: _root;;
	*);;

	-> rrel_operators: ...(*
		
		-> rrel_init: ..print1 (*
			<- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [Процедура поиска корневого узла. Вход];;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_1;;
	   	*);;

	   	-> .proc_of_simply_the_sc_design_root_1 (*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: rrel_const: _nodes;;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_2;;
	   	*);;

		-> .proc_of_simply_the_sc_design_root_2 (*
			<- searchSetStr5;;
			-> rrel_1: rrel_assign: rrel_scp_var: _el1;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _el2;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _contour;;

			-> rrel_set_3: rrel_fixed: rrel_scp_var: _nodes;;
			
			=> nrel_goto: .proc_of_simply_the_sc_design_root_3;;
		*);;

		-> .proc_of_simply_the_sc_design_root_3 (*
			<- contAssign;;
			-> rrel_1: rrel_assign: rrel_scp_var: _i;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_4;;
		*);;

		-> .proc_of_simply_the_sc_design_root_4 (*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: number;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _i;;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_5;;
		*);;

		-> .proc_of_simply_the_sc_design_root_5 (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _nodes;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _root;;
			=> nrel_then: .proc_of_simply_the_sc_design_root_6;;
			=> nrel_else: .proc_of_simply_the_sc_design_root_9;;
		*);;

		-> .proc_of_simply_the_sc_design_root_6 (*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc;;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_7;;
		*);;

		-> .proc_of_simply_the_sc_design_root_7 (*
			<- contAdd;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _i;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _i;;
			-> rrel_3: rrel_fixed: rrel_scp_const: [1];;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_8;;
		*);;

		-> .proc_of_simply_the_sc_design_root_8 (*
			<- searchElStr5;;
			-> rrel_1: rrel_assign: rrel_scp_var: _el1;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _root;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _contour;;
			=> nrel_then: .proc_of_simply_the_sc_design_root_5;;		
			=> nrel_else: ..print4;;
		*);;

		-> .proc_of_simply_the_sc_design_root_9 (*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _nodes;;
			=> nrel_goto: ..print4;;
		*);;

		-> ..print4 (*
			<- print;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [Корневой узел, узел равенства найден ! Количество итераций: ];;
			=> nrel_goto: ..print5;;
		*);;

		-> ..print5 (*
			<- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _i;;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_10;;
		*);;
		
		-> .proc_of_simply_the_sc_design_root_10 (*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _state;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: rrel_const: _true;;
			=> nrel_goto: ..print2;;
		*);;

	    -> ..print2 (*
			<- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [Процедура поиска корневого узла. Выход];;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_return;;
		*);;

		-> ..print3 (*
			<- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [Процедура поиска корневого узла завершена с ошибкой];;
			=> nrel_goto: .proc_of_simply_the_sc_design_root_return;;
		*);;

		-> .proc_of_simply_the_sc_design_root_return (*
			<- return;;
		*);;
	*);;
*);;