scp_program -> proc_binary_tree_set_value (*	
	
	-> rrel_params: ... (*

		// параметры бинарного дерева
		-> rrel_1: rrel_in: _in_parameter;;

		// изменяемый входный параметр: текущий узел дерева
		-> rrel_2: rrel_in: _element;;

		// изменяемый входный параметр: сохраняемое значение
		-> rrel_3: rrel_in: _value;;
	*);;

	-> rrel_operators: ...(*

		-> rrel_init: .proc_binary_tree_set_value_0 (*
			<- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [Сохранение элемента. Вход];;
			=> nrel_goto: .proc_binary_tree_set_value_A;;
		*);;

		-> .proc_binary_tree_set_value_A (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _in_parameter;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _contour;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;
			=> nrel_goto: .proc_binary_tree_set_value_B;;
		*);;

		-> .proc_binary_tree_set_value_B (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _in_parameter;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _out_4;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_4;;
			=> nrel_goto: .proc_binary_tree_set_value_C;;
		*);;

		-> .proc_binary_tree_set_value_C (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _out_4;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _nrel_value;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;
			=> nrel_goto: .proc_binary_tree_set_value_1;;
		*);;

		-> .proc_binary_tree_set_value_1 (*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _element;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: rrel_var: _arc_element_value;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _value;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos: rrel_var: rrel_perm: _arc_value_nrel;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _nrel_value;;
			=> nrel_goto: .proc_binary_tree_set_value_2;;
		*);;

		-> .proc_binary_tree_set_value_2 (*
			<- ifType;;
			-> rrel_1: rrel_const: _value;;
			=> nrel_then: .proc_binary_tree_set_value_3;;
			=> nrel_else: ..print2;;
		*);;

		-> .proc_binary_tree_set_value_3 (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _contour;;
			-> rrel_2: rrel_assign: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _value;;
			=> nrel_then: .proc_binary_tree_set_value_5;;
			=> nrel_else: .proc_binary_tree_set_value_4;;
		*);;

		-> .proc_binary_tree_set_value_4 (*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _contour;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _value;;
			=> nrel_goto: .proc_binary_tree_set_value_5;;
		*);;

		-> .proc_binary_tree_set_value_5 (*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _contour;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _arc_element_value;;
			=> nrel_goto: .proc_binary_tree_set_value_6;;
		*);;

		-> .proc_binary_tree_set_value_6 (*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _contour;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _arc_value_nrel;;
			=> nrel_goto: ..print2;;
		*);;

	    -> ..print2 (*
			<- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [Сохранение элемента. Выход];;
			=> nrel_goto: .proc_binary_tree_set_value_return;;
		*);;

		-> .proc_binary_tree_set_value_return (*
			<- return;;
		*);;
	*);;
*);;